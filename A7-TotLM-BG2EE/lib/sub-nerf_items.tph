// Subcomponent: Nerf selected weapons and equipment

// Items to be replaced or removed:
// AXEMINO.ITM (Axe of the Minotaur Lord +4): minlrd.cre
//  - AX1H11.ITM
// BRAC10.ITM (Legacy of the Masters): harald.cre
//  - BRAC09.ITM
// BRACEIP.ITM (Bracers of Icelandic Pearl): hobart.sto
//  - BRAC14.ITM (BRAC03.ITM)
// CLCK17.ITM (Robe of the Evil Archmagi): jklsum.cre
//  - CLCK12.ITM
// CLOAKSC.ITM (Cloak of Scintillating Colors): adran.cre
//  - remove completely
// FLAILSK.ITM (Skullflail +4): hobart.sto
//  - BLUN13.ITM
// HELMCT.ITM (Chautog's Thinker): hrpmat.cre
//  - HELM16.ITM (HELM03.ITM)
// ISWARM.ITM (Black Swan Armor +2): criek.cre
//  - PLAT10.ITM
// MISC07.ITM (Gold): id9800.are > Container 6 (name: "Chest 2")
//  - MISC23.ITM
// SBOWEBU.ITM (Shortbow of Ebullience +3): hobart.sto
//  - BOW14.ITM (BOW08.ITM)
// SW1H15#.ITM (Scimitar +3, Frostbrand): id9800.are > Container 5 (name: "Chest 1")
//  - SW1H22.ITM
// UBSWD5B.ITM (Bastard Sword +3: Incinerator): id9714.are > Container 32 (name: "Corpse Shelf")
//  - SW1H37.ITM (SW1H42.ITM)
// UFLAL4A.ITM (Fire Flail +3): criek.cre
//  - BLUN18.ITM (BLUN13.ITM)
// UFLAL5A.ITM (Shocking Flail +4): id9714.are > Container 11 (name: "Treasure Sarcophogus")
//  - BLUN32.ITM (BLUN13.ITM)
// ULSWD5A.ITM (Long Sword of Action +4): id9714.are > Container 1 (name: "Geddian's Sarcophogus"), mercwr2d.cre
//  - SW1H33.ITM, SW1H41.ITM
// UMSTR5B.ITM (Morning Star +4: Defender): harald.cre
//  - BLUN34.ITM (BLUN39.ITM)
// URING5A.ITM (Ring of Greater Resistance): criek.cre
//  - RING06.ITM
// USHLD3B.ITM (Great Shield +3): id9712.are > Container 1 (name: "Container 2")
//  - SHLD06.ITM
// USLTR4A.ITM (Studded Leather of Resistance +3): id9714.are > Container 11 (name: "Treasure Sarcophogus")
//  - LEAT15.ITM
// USLTR5A.ITM (Shadowed Studded Leather +4): mercthfd.cre
// - LEAT12.ITM
// ZZ36DGD.ITM (Dead God's Dreaming +4, +6 vs. good): lordm.cre
//  - SW1H35.ITM (SW1H24.ITM)
// ZZC8HB.ITM (Hell's Bane): id9800.are > Container 9 (name: "Rack 1")
//  - SW2H12.ITM (BDSW2H03.ITM)
// ZZD5WOH.ITM (Wind of Heaven +4, +5 vs. elementals): djinni#.cre
//  - SW1H23.ITM

COPY_EXISTING ~minlrd.cre~ ~override~
  // Axe of the Minotaur Lord +4
  REMOVE_CRE_ITEM ~axemino~
  ADD_CRE_ITEM ~ax1h11~ #0 #0 #0 ~none~ ~weapon1 weapon2 weapon3 weapon4~ EQUIP
BUT_ONLY IF_EXISTS

COPY_EXISTING ~adran.cre~ ~override~
  // Cloak of Scintillating Colors
  REMOVE_CRE_ITEM ~cloaksc~
BUT_ONLY IF_EXISTS

COPY_EXISTING ~hrpmat.cre~ ~override~
  // Chautog's Thinker
  REMOVE_CRE_ITEM ~helmct~
  PATCH_IF (FILE_EXISTS_IN_GAME ~helm16.itm~) BEGIN
    ADD_CRE_ITEM ~helm16~ #1 #1 #1 ~none~ ~helmet inv1 inv2 inv3 inv4~
  END ELSE BEGIN
    ADD_CRE_ITEM ~helm03~ #1 #1 #1 ~none~ ~helmet inv1 inv2 inv3 inv4~
  END
BUT_ONLY IF_EXISTS

COPY_EXISTING ~criek.cre~ ~override~
  // Black Swan Armor +2
  REMOVE_CRE_ITEM ~iswarm~
  ADD_CRE_ITEM ~plat10~ #0 #0 #0 ~none~ ~armor inv1 inv2 inv3 inv4~
  // Fire Flail +3
  REMOVE_CRE_ITEM ~uflal4a~
  PATCH_IF (FILE_EXISTS_IN_GAME ~blun18.itm~) BEGIN
    ADD_CRE_ITEM ~blun18~ #0 #0 #0 ~none~ ~weapon1 weapon2 weapon3 weapon4~ EQUIP
  END ELSE BEGIN
    ADD_CRE_ITEM ~blun13~ #0 #0 #0 ~none~ ~weapon1 weapon2 weapon3 weapon4~ EQUIP
  END
  // Ring of Greater Resistance
  REMOVE_CRE_ITEM ~uring5a~
  ADD_CRE_ITEM ~ring06~ #0 #0 #0 ~none~ ~lring rring inv1 inv2 inv3 inv4~
BUT_ONLY IF_EXISTS

COPY_EXISTING ~mercwr2d.cre~ ~override~
  // Long Sword of Action +4
  REMOVE_CRE_ITEM ~ulswd5a~
  ADD_CRE_ITEM ~sw1h41~ #0 #0 #0 ~none~ ~weapon1 weapon2 weapon3 weapon4~ EQUIP
BUT_ONLY IF_EXISTS

COPY_EXISTING ~harald.cre~ ~override~
  // Legacy of the Masters
  REMOVE_CRE_ITEM ~brac10~
  ADD_CRE_ITEM ~brac09~ #0 #0 #0 ~none~ ~gloves inv1 inv2 inv3 inv4~
  // Morning Star +4: Defender
  REMOVE_CRE_ITEM ~umstr5b~
  PATCH_IF (FILE_EXISTS_IN_GAME ~blun34.itm~) BEGIN
    ADD_CRE_ITEM ~blun34~ #0 #0 #0 ~none~ ~weapon1 weapon2 weapon3 weapon4~ EQUIP
  END ELSE BEGIN
    ADD_CRE_ITEM ~blun39~ #0 #0 #0 ~none~ ~weapon1 weapon2 weapon3 weapon4~ EQUIP
  END
BUT_ONLY IF_EXISTS

COPY_EXISTING ~jklsum.cre~ ~override~
  // Robe of the Evil Archmagi
  REMOVE_CRE_ITEM ~clck17~
  ADD_CRE_ITEM ~clck12~ #0 #0 #0 ~none~ ~armor inv1 inv2 inv3 inv4~
BUT_ONLY IF_EXISTS

COPY_EXISTING ~mercthfd.cre~ ~override~
  // Shadowed Studded Leather +4
  REMOVE_CRE_ITEM ~usltr5a~
  ADD_CRE_ITEM ~leat12~ #0 #0 #0 ~none~ ~armor inv1 inv2 inv3 inv4~
BUT_ONLY IF_EXISTS

COPY_EXISTING ~lordm.cre~ ~override~
  // Dead God's Dreaming +4, +6 vs. good
  REMOVE_CRE_ITEM ~zz36dgd~
  PATCH_IF (FILE_EXISTS_IN_GAME ~sw1h35.itm~) BEGIN
    ADD_CRE_ITEM ~sw1h35~ #0 #0 #0 ~none~ ~weapon1 weapon2 weapon3 weapon4~ EQUIP
  END ELSE BEGIN
    ADD_CRE_ITEM ~sw1h24~ #0 #0 #0 ~none~ ~weapon1 weapon2 weapon3 weapon4~ EQUIP
  END
BUT_ONLY IF_EXISTS

COPY_EXISTING ~djinni#.cre~ ~override~
  // Wind of Heaven +4, +5 vs. elementals
  REMOVE_CRE_ITEM ~zzd5woh~
  ADD_CRE_ITEM ~sw1h23~ #0 #0 #0 ~none~ ~weapon1 weapon2 weapon3 weapon4~ EQUIP
BUT_ONLY IF_EXISTS

COPY_EXISTING ~hobart.sto~ ~override~
  // Bracers of Icelandic Pearl
  PATCH_IF (FILE_EXISTS_IN_GAME ~brac14.itm~) BEGIN
    TEXT_SPRINT new_item ~brac14~
  END ELSE BEGIN
    TEXT_SPRINT new_item ~brac03~
  END
  LPF REPLACE_STORE_ITEM
  INT_VAR
    number_in_stock = 1
    charges1        = 1
    charges2        = 1
    charges3        = 1
  STR_VAR
    old_item  = ~braceip~
    new_item
    flags     = ~identified~
  END
  // Skullflail +4
  LPF REPLACE_STORE_ITEM
  INT_VAR
    number_in_stock = 1
    charges1        = 0
    charges2        = 0
    charges3        = 0
  STR_VAR
    old_item  = ~flailsk~
    new_item  = ~blun13~
    flags     = ~identified~
  END
  // Shortbow of Ebullience +3
  PATCH_IF (FILE_EXISTS_IN_GAME ~bow14.itm~) BEGIN
    TEXT_SPRINT new_item ~bow14~
  END ELSE BEGIN
    TEXT_SPRINT new_item ~bow08~
  END
  LPF REPLACE_STORE_ITEM
  INT_VAR
    number_in_stock = 1
    charges1        = 0
    charges2        = 0
    charges3        = 0
  STR_VAR
    old_item  = ~sbowebu~
    new_item
    flags     = ~identified~
  END
BUT_ONLY IF_EXISTS

COPY_EXISTING ~id9714.are~ ~override~
  // Bastard Sword +3: Incinerator
  PATCH_IF (FILE_EXISTS_IN_GAME ~sw1h37.itm~) BEGIN
    TEXT_SPRINT new_item ~sw1h37~
  END ELSE BEGIN
    TEXT_SPRINT new_item ~sw1h42~
  END
  LPF REPLACE_AREA_ITEM
  STR_VAR
    old_item  = ~ubswd5b~
    new_item
  END
  // Shocking Flail +4
  PATCH_IF (FILE_EXISTS_IN_GAME ~blun32.itm~) BEGIN
    TEXT_SPRINT new_item ~blun32~
  END ELSE BEGIN
    TEXT_SPRINT new_item ~blun13~
  END
  LPF REPLACE_AREA_ITEM
  STR_VAR
    old_item  = ~uflal5a~
    new_item
  END
  // Long Sword of Action +4
  PATCH_IF (FILE_EXISTS_IN_GAME ~sw1h33.itm~) BEGIN
    TEXT_SPRINT new_item ~sw1h33~
  END ELSE BEGIN
    TEXT_SPRINT new_item ~sw1h41~
  END
  LPF REPLACE_AREA_ITEM
  STR_VAR
    old_item  = ~ulswd5a~
    new_item
  END
  // Studded Leather of Resistance +3
  LPF REPLACE_AREA_ITEM
  STR_VAR
    old_item  = ~usltr4a~
    new_item  = ~leat15~
  END
BUT_ONLY IF_EXISTS

COPY_EXISTING ~id9712.are~ ~override~
  // Great Shield +3
  LPF REPLACE_AREA_ITEM
  STR_VAR
    old_item  = ~ushld3b~
    new_item  = ~shld06~
  END
BUT_ONLY IF_EXISTS

COPY_EXISTING ~id9800.are~ ~override~
  // Gold
  LPF REPLACE_AREA_ITEM
  INT_VAR
    charges1  = 1
  STR_VAR
    old_item  = ~misc07~
    new_item  = ~misc23~
  END
  // Scimitar +3, Frostbrand
  LPF REPLACE_AREA_ITEM
  STR_VAR
    old_item  = ~sw1h15#~
    new_item  = ~sw1h22~
  END
  // Hell's Bane
  PATCH_IF (FILE_EXISTS_IN_GAME ~sw2h12.itm~) BEGIN
    TEXT_SPRINT new_item ~sw2h12~
  END ELSE BEGIN
    TEXT_SPRINT new_item ~bdsw2h03~
  END
  LPF REPLACE_AREA_ITEM
  STR_VAR
    old_item  = ~zzc8hb~
    new_item
  END
BUT_ONLY IF_EXISTS


// Additional replacements for SoD
// scrl9b.itm (Summon Fiend, level 8)
// - scrl8a.itm (Carrion Summons, level 6)
// scrl9c.itm (Improved Mantle, level 8)
// - scrl7o.itm (Protection From Magical Weapons, level 6)
// scrl9n.itm (Gate, level 9)
// - scrl7e.itm (Invisible Stalker, level 6)
// scrl9q (Chain Contingency, level 9)
// - scrl7u.itm (Contingency, level 6)
// scrl9u (Power Word, Kill, level 9)
// - scrl7r.itm (Death Fog, 6)
// scrlap (Symbol, Stun, level 8)
// - scrl7q.itm (Improved Haste, level 6)
ACTION_IF (GAME_IS ~bgee~ && GAME_INCLUDES ~sod~) BEGIN
  COPY_EXISTING ~id9712.are~ ~override~
                ~id9800.are~ ~override~
    LPF REPLACE_AREA_ITEM
    STR_VAR
      old_item = ~scrl9b~
      new_item = ~scrl8a~
    END
  BUT_ONLY IF_EXISTS

  COPY_EXISTING ~id9714.are~ ~override~
    LPF REPLACE_AREA_ITEM
    STR_VAR
      old_item = ~scrl9c~
      new_item = ~scrl7o~
    END
  BUT_ONLY IF_EXISTS

  COPY_EXISTING ~id9717.are~ ~override~
    LPF REPLACE_AREA_ITEM
    STR_VAR
      old_item = ~scrl9n~
      new_item = ~scrl7e~
    END
  BUT_ONLY IF_EXISTS

  COPY_EXISTING ~id9712.are~ ~override~
    LPF REPLACE_AREA_ITEM
    STR_VAR
      old_item = ~scrl9q~
      new_item = ~scrl7u~
    END
    LPF REPLACE_AREA_ITEM
    STR_VAR
      old_item = ~scrl9u~
      new_item = ~scrl7r~
    END
  BUT_ONLY IF_EXISTS

  COPY_EXISTING ~bagh02.sto~ ~override~
    LPF REPLACE_STORE_ITEM
    INT_VAR
      number_in_stock = 1
      charges1        = 1
      charges2        = 0
      charges3        = 0
    STR_VAR
      old_item  = ~scrlap~
      new_item  = ~scrl7q~
      flags     = ~identified~
    END
  BUT_ONLY IF_EXISTS
END

// Subcomponent: Nerf selected weapons and equipment

// Items to be replaced or removed:
// AXEMINO.ITM (Axe of the Minotaur Lord +4): minlrd.cre
//  - AX1H11.ITM
// BRACEIP.ITM (Bracers of Icelandic Pearl): hobart.sto
//  - BRAC14.ITM
// FLAILSK.ITM (Skullflail +4): hobart.sto
//  - BLUN13.ITM
// HELMCT.ITM (Chautog's Thinker): hrpmat.cre
//  - HELM16.ITM
// SBOWEBU.ITM (Shortbow of Ebullience +3): hobart.sto
// - BOW14.ITM
// UBSWD5B.ITM (Bastard Sword +3: Incinerator): id9714.are > Container 32 (name: "Corpse Shelf")
//  - SW1H37.ITM
// UFLAL4A.ITM (Fire Flail +3): criek.cre
//  - BLUN18.ITM
// UFLAL5A.ITM (Shocking Flail +4): id9714.are > Container 11 (name: "Treasure Sarcophogus")
//  - BLUN32.ITM
// ULSWD5A.ITM (Long Sword of Action +4): id9714.are > Container 1 (name: "Geddian's Sarcophogus"), mercwr2d.cre
// - SW1H33.ITM, SW1H41.ITM
// UMSTR5B.ITM (Morning Star +4: Defender): harald.cre
//  - BLUN34.ITM
// URING5A.ITM (Ring of Greater Resistance): criek.cre, hrpmat.cre
//  - RING06.ITM
// USHLD3B.ITM (Great Shield +3): id9712.are > Container 1 (name: "Container 2")
//  - SHLD06.ITM
// USLTR4A.ITM (Studded Leather of Resistance +3): id9714.are > Container 11 (name: "Treasure Sarcophogus")
//  - LEAT15.ITM
// USLTR5A.ITM (Shadowed Studded Leather +4): mercthfd.cre
// - LEAT12.ITM
// ZZ36DGD.ITM (Dead God's Dreaming +4, +6 vs. good): lordm.cre
//  - SW1H35.ITM
// ZZC8HB.ITM (Hell's Bane): id9800.are > Container 9 (name: "Rack 1")
//  - SW2H12.ITM
// ZZD5WOH.ITM (Wind of Heaven +4, +5 vs. elementals): djinni_.cre
//  - SW1H23.ITM

COPY_EXISTING ~minlrd.cre~ ~override~
  // Axe of the Minotaur Lord +4
  REMOVE_CRE_ITEM ~axemino~
  ADD_CRE_ITEM ~ax1h11~ #0 #0 #0 ~none~ ~weapon1 weapon2 weapon3 weapon4~ EQUIP
BUT_ONLY IF_EXISTS

COPY_EXISTING ~hrpmat.cre~ ~override~
  // Chautog's Thinker
  REMOVE_CRE_ITEM ~helmct~
  ADD_CRE_ITEM ~helm16~ #1 #1 #1 ~none~ ~helmet inv1 inv2 inv3 inv4~
  // Ring of Greater Resistance
  REMOVE_CRE_ITEM ~uring5a~
  ADD_CRE_ITEM ~ring06~ #0 #0 #0 ~none~ ~lring rring inv1 inv2 inv3 inv4~
BUT_ONLY IF_EXISTS

COPY_EXISTING ~criek.cre~ ~override~
  // Fire Flail +3
  REMOVE_CRE_ITEM ~uflal4a~
  ADD_CRE_ITEM ~blun18~ #0 #0 #0 ~none~ ~weapon1 weapon2 weapon3 weapon4~ EQUIP
  // Ring of Greater Resistance
  REMOVE_CRE_ITEM ~uring5a~
  ADD_CRE_ITEM ~ring06~ #0 #0 #0 ~none~ ~lring rring inv1 inv2 inv3 inv4~
BUT_ONLY IF_EXISTS

COPY_EXISTING ~mercwr2d.cre~ ~override~
  // Long Sword of Action +4
  REMOVE_CRE_ITEM ~ulswd5a~
  ADD_CRE_ITEM ~sw1h41~ #0 #0 #0 ~none~ ~weapon1 weapon2 weapon3 weapon4~ EQUIP
BUT_ONLY IF_EXISTS

COPY_EXISTING ~harald.cre~ ~override~
  // Morning Star +4: Defender
  REMOVE_CRE_ITEM ~umstr5b~
  ADD_CRE_ITEM ~blun34~ #0 #0 #0 ~none~ ~weapon1 weapon2 weapon3 weapon4~ EQUIP
BUT_ONLY IF_EXISTS

COPY_EXISTING ~mercthfd.cre~ ~override~
  // Shadowed Studded Leather +4
  REMOVE_CRE_ITEM ~usltr5a~
  ADD_CRE_ITEM ~leat12~ #0 #0 #0 ~none~ ~armor inv1 inv2 inv3 inv4~
BUT_ONLY IF_EXISTS

COPY_EXISTING ~lordm.cre~ ~override~
  // Dead God's Dreaming +4, +6 vs. good
  REMOVE_CRE_ITEM ~zz36dgd~
  ADD_CRE_ITEM ~sw1h35~ #0 #0 #0 ~none~ ~weapon1 weapon2 weapon3 weapon4~ EQUIP
BUT_ONLY IF_EXISTS

COPY_EXISTING ~djinni_.cre~ ~override~
  // Wind of Heaven +4, +5 vs. elementals
  REMOVE_CRE_ITEM ~zzd5woh~
  ADD_CRE_ITEM ~sw1h23~ #0 #0 #0 ~none~ ~weapon1 weapon2 weapon3 weapon4~ EQUIP
BUT_ONLY IF_EXISTS

COPY_EXISTING ~hobart.sto~ ~override~
  // Bracers of Icelandic Pearl
  LPF REPLACE_STORE_ITEM
  INT_VAR
    number_in_stock = 1
    charges1        = 1
    charges2        = 1
    charges3        = 1
  STR_VAR
    old_item  = ~braceip~
    new_item  = ~brac14~
    flags     = ~identified~
  END
  // Skullflail +4
  LPF REPLACE_STORE_ITEM
  INT_VAR
    number_in_stock = 1
    charges1        = 0
    charges2        = 0
    charges3        = 0
  STR_VAR
    old_item  = ~flailsk~
    new_item  = ~blun13~
    flags     = ~identified~
  END
  // Shortbow of Ebullience +3
  LPF REPLACE_STORE_ITEM
  INT_VAR
    number_in_stock = 1
    charges1        = 0
    charges2        = 0
    charges3        = 0
  STR_VAR
    old_item  = ~sbowebu~
    new_item  = ~bow14~
    flags     = ~identified~
  END
BUT_ONLY IF_EXISTS

COPY_EXISTING ~id9714.are~ ~override~
  // Bastard Sword +3: Incinerator
  LPF REPLACE_AREA_ITEM
  STR_VAR
    old_item  = ~ubswd5b~
    new_item  = ~sw1h37~
  END
  // Shocking Flail +4
  LPF REPLACE_AREA_ITEM
  STR_VAR
    old_item  = ~uflal5a~
    new_item  = ~blun32~
  END
  // Long Sword of Action +4
  LPF REPLACE_AREA_ITEM
  STR_VAR
    old_item  = ~ulswd5a~
    new_item  = ~sw1h33~
  END
  // Studded Leather of Resistance +3
  LPF REPLACE_AREA_ITEM
  STR_VAR
    old_item  = ~usltr4a~
    new_item  = ~leat15~
  END
BUT_ONLY IF_EXISTS

COPY_EXISTING ~id9712.are~ ~override~
  // Great Shield +3
  LPF REPLACE_AREA_ITEM
  STR_VAR
    old_item  = ~ushld3b~
    new_item  = ~shld06~
  END
BUT_ONLY IF_EXISTS

COPY_EXISTING ~id9800.are~ ~override~
  // Hell's Bane
  LPF REPLACE_AREA_ITEM
  STR_VAR
    old_item  = ~zzc8hb~
    new_item  = ~sw2h12~
  END
BUT_ONLY IF_EXISTS
